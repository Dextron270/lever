# This file describes & implements the syntax for the lever language.

# The grammar language is quite simple.
# It resolves into context-free-grammars annotated with call patterns.
# You can define a function that should evaluate with xxx{...}
# Alternating right-hand-side rules are separated by newline.
# Function is called with the stuff that is grabbed from inside the
# parentheses.
# Nonterminals and symbolic terminals are grabbed by default.
# Keywords denoted with "" are skipped by default.
# ^ prefix may force a grab.
# % prefix may force a skip.

file =>
    empty_list {}
    statements

block =>
    {%@indent statements %@dedent}

statements =>
    first  {block_statement}
    append {statements %@newline block_statement}

block_statement =>
    statement
    return {"return" statement}
    return {"return"}
    yield {"yield" statement}
    if     {"if"     statement block otherwise}
    while  {"while"  statement block}
    local_assign   {local_symbol      "="  block_statement}
    upvalue_assign {@symbol           ":=" block_statement}
    op_assign      {slot op           "="  block_statement}
    setitem        {expr "[" subexpr "]" "=" block_statement}
    setattr        {expr "." @symbol  "="  block_statement}
    for      {"for" @symbol "in" subexpr block}
    import   {"import" symbols_list}
    from_import     {"from" @symbol "import" symbols_list}
    from_import_all {"from" @symbol "import" "*"}
    try      {"try" block excepts}
    try      {"try" block excepts_opt finally}
    raise    {"raise" statement}
    break    {"break"}
    continue {"continue"}
    class    {"class" class_header}
    class    {"class" class_header block}
    assert   {"assert" statement "," statement}
    assert   {"assert" statement block}
    assert   {"assert" statement}

class_header =>
    class_header {@symbol}
    class_header {@symbol "extends" expr}

local_symbol =>
    str_join {@symbol}
    str_join {^"+" @symbol}
    str_join {"%" @string}

slot =>
    lookup_slot {@symbol}
    attr_slot   {expr "." @symbol}
    item_slot   {expr "[" expr "]"}

otherwise =>
    done {}
    elif {%@newline "elif" statement block otherwise}
    else {%@newline "else" block}

excepts_opt =>
    empty_list {}
    excepts

excepts =>
    first  {except}
    append {excepts %@newline except}

except =>
    except {"except" expr "as" @symbol block}

finally =>
    finally {"finally" block}

statement =>
    expr

subexpr =>
    expr
    slice_incr { opt_expr  ".:" opt_expr }
    slice_incr { opt_expr  ".:" opt_expr "::" expr }
    slice_decr { opt_expr  ":." opt_expr }
    slice_decr { opt_expr  ":." opt_expr "::" expr }

opt_expr =>
    nil{}
    expr

expr =>
    expr3
    or {expr3 "or" expr}

expr3 =>
    expr5
    and {expr5 "and" expr3}

expr5 =>
    expr8
    not {"not" expr8}

expr8 =>
    expr10
    in     {expr10 "in" expr10}
    not_in {expr10 "not" "in" expr10}
    binary {expr10 ^"<" expr10}
    binary {expr10 ^">" expr10}
    binary {expr10 ^"==" expr10}
    binary {expr10 ^"!=" expr10}
    binary {expr10 ^"<=" expr10}
    binary {expr10 ^">=" expr10}

expr10 =>
    expr20
    binary {expr10 ^"|" expr20}

expr20 =>
    expr30
    binary {expr20 ^"^" expr30}

expr30 =>
    expr50
    binary {expr30 ^"&" expr50}

expr50 =>
    expr100
    binary {expr50 ^"<<" expr100}
    binary {expr50 ^">>" expr100}

expr100 =>
    expr200
    binary {expr100 ^"++" expr200}
    binary {expr100 ^"+" expr200}
    binary {expr100 ^"-" expr200}

expr200 =>
    prefix
    binary {expr200 ^"*" prefix}
    binary {expr200 ^"/" prefix}
    binary {expr200 ^"%" prefix}

prefix =>
    postfix
    prefix {^"+" postfix}
    prefix {^"-" postfix}

op => ["|", "^", "&", "<<", ">>", "++", "+", "-", "%", "/", "*"]

# here we should still have bitwise not

postfix =>
    term
    call  {postfix "(" arguments ")"}
    callv {postfix "(" arguments "..." ")"}
    getitem {postfix "[" subexpr "]"}
    getattr {postfix "." @symbol}

arguments =>
    empty_list {}
    arguments1
    {arguments1 ","}

arguments1 =>
    first  {subexpr}
    append {arguments1 "," subexpr}

term =>
    lookup {@symbol}
    int {@int}
    hex {@hex}
    float  {@float}
    string {@string}
    {"(" subexpr ")"}
    list     {"[" arguments "]"}
    function {"(" bindings ")" ":" block}
    function {blank_bindings{} ":" block}
    dict     {"{" pairs "}"}
    dict     {"{" nl_pairs "}"}
    lookup   {"{" escaped_keyword "}"}
    lookup   {"%" @string}
    scopegrabber {":" expr block}

# This may end up ambiguous within a function call.
# Introduced this rule to allow nesting of functions.
#block_or_statement =>
#    block
#    first { return { block_statement } }
#
# There was a problem with this. If there is no
# statement it will pick the subsequent line.

bindings =>
    blank_bindings {}
    optionals
    optionals ","
    with_variadic {optionals "," @symbol "..."}
    only_variadic {@symbol "..."}

optionals =>
    first_optional {optional}
    append_optional {optionals "," optional}
    mandatorys

optional =>
    optional {@symbol "=" expr}

mandatorys =>
    mandatory {@symbol}
    append_mandatory {mandatorys "," @symbol}

nl_pairs => %@indent pairlines %@dedent

pairlines =>
    first  {pair}
    append {pairlines %@newline pair}
    first  {pair ","}
    append {pairlines %@newline pair ","}

pairs =>
    empty_list {}
    pairs1
    pairs1 ","

pairs1 =>
    first  {pair}
    append {pairs1 "," pair}

pair =>
    {expr ":" expr}
    {string {@symbol} "=" expr}

escaped_keyword => ["import", "and", "or", "not"]

symbols_list =>
    first  {@symbol}
    append {symbols_list "," @symbol}
