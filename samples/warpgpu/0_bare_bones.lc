import sdl
import vulkan, ffi, api, fs
from warpgpu import *

main = ():
    window = sdl.Window(name, {
        width = 512
        height = 512
    })
    gpu = GPU(window.wsi)
    screen = Rect(0, 0, window.width, window.height)
    swapchain = get_swapchain(gpu.device, window.wsi.surface, screen)

    vertex_layout = object();
        vertexInputState = {
            flags = 0
            vertexBindingDescriptions = [{
                binding = 0
                stride = 6 * ffi.sizeof(ffi.float)
                inputRate = "VERTEX"
            }]
            vertexAttributeDescriptions = [
                {
                    binding = 0
                    location = 0
                    format = "R32G32B32_SFLOAT"
                    offset = 0
                },
                {
                    binding = 0
                    location = 1
                    format = "R32G32B32_SFLOAT"
                    offset = 3 * ffi.sizeof(ffi.float)
                }
            ]
        }
        inputAssemblyState = {topology = "TRIANGLE_LIST"}

    vbo = object();
        size = 8 * 1024
        buffer = gpu.mem.createBuffer(size, "VERTEX_BUFFER_BIT")
        data = buffer.mem.map(ffi.float, buffer.offset, buffer.size)
        stuff = [
            0.0, 0.0, 0.0, 1.0, 0.5, 0.5,
            0.0, 1.0, 0.0, 0.5, 1.0, 0.5,
            1.0, 0.0, 0.0, 0.5, 0.5, 1.0,
            0.0, 1.0, 0.0, 0.5, 0.5, 1.0,
            1.0, 1.0, 0.0, 0.5, 1.0, 1.0,
            1.0, 0.0, 0.0, 1.0, 1.0, 0.5,
        ]
        for i in range(stuff.length)
            data[i] = stuff[i]
        buffer.mem.unmap()

    ubo = object();
        size = 2 * 4 * 4 * ffi.sizeof(ffi.float)
        buffer = gpu.mem.createBuffer(size, "UNIFORM_BUFFER_BIT")
        data = buffer.mem.map(ffi.float, buffer.offset, buffer.size)
        stuff = [
            1.0, 0.0, 0.0, 0.0,
            0.0, 1.0, 0.0, 0.0,
            0.0, 0.0, 1.0, 0.0,
           -0.2,-0.2, 0.0, 1.0,

            1.0, 0.0, 0.0, 0.0,
            0.0, 1.0, 0.0, 0.0,
            0.0, 0.0, 1.0, 0.0,
            0.0, 0.0, 0.0, 1.0]
        for i in range(stuff.length)
            data[i] = stuff[i]
        buffer.mem.unmap()

    cbuf = gpu.queue.pool.allocate({level = "PRIMARY", commandBufferCount = 1})[0]

    DSC0 = DescriptorSetLayout(gpu.device, {
        "testbuffer": {
            binding=0,
            descriptorType = "UNIFORM_BUFFER",
            stageFlags="VERTEX_BIT"
        }
    })
    # DEP: pipeline_layout, descset

    dsc0 = DSC0()
    dsc0.testbuffer = {
        buffer = ubo.buffer
        offset = 0
        range  = -1#ubo.buffer.size
    }
    gpu.update(dsc0)
    # DEP: cbuf

    pipeline_layout = vulkan.PipelineLayout(gpu.device, {
        flags = 0
        setLayouts = [DSC0]
        pushConstantRanges = []
    })
    # # DEP: cbuf, pipeline

    pb = RenderPassBuilder()
    pb.color0.attachment(swapchain.config, {
        loadOp = "CLEAR"
        storeOp = "STORE"
        initialLayout = 0 #UNDEFINED
        finalLayout   = "PRESENT_SRC_KHR"
    })
    pb.p0.subpass("GRAPHICS", {
        colorAttachments = [
            pb.color0("COLOR_ATTACHMENT_OPTIMAL")
        ]
    })

    # When the external pass dependencies are not provided, they should be inserted
    # implicitly. Added here for completeness.
    external_pass = (subpass=gpu.vk.SUBPASS_EXTERNAL)
    pb.dependency(external_pass, pb.p0, {
        srcStageMask = "COLOR_ATTACHMENT_OUTPUT_BIT" # default would be "TOP_OF_PIPE_BIT"
        dstStageMask = "COLOR_ATTACHMENT_OUTPUT_BIT" # default would be "ALL_COMMANDS_BIT"
        srcAccessMask = []
        dstAccessMask = [
            "COLOR_ATTACHMENT_READ_BIT",   # default would contain: INPUT/COLOR/DEPTH_ATTACHMENT_READ_BIT
            "COLOR_ATTACHMENT_WRITE_BIT" ] #                              COLOR/DEPTH_ATTACHMENT_WRITE_BIT
    })
    pb.dependency(pb.p0, external_pass, {
        srcStageMask = "ALL_COMMANDS_BIT"   # default
        dstStageMask = "BOTTOM_OF_PIPE_BIT" # default
        srcAccessMask = [
            "COLOR_ATTACHMENT_READ_BIT",   # default would again contain lot more than this.
            "COLOR_ATTACHMENT_WRITE_BIT" ]
        dstAccessMask = []
    })

    pass = pb.build(gpu.device, screen)
    # DEP: framebuffer, pipeline, cbuf

    swapchain.views = []
    swapchain.framebuffers = []
    for image in swapchain.images
        view = vulkan.ImageView(gpu.device, {
            image = image
            viewType = "2D"
            format = swapchain.format
            components = {r = "R", g = "G", b = "B", a = "A"}
            subresourceRange = swapchain.subresource_range
        })
        swapchain.views.append(view)
        # DEP: framebuffer

        framebuffer = pass.Framebuffer({
            color0 = view
        })
        swapchain.framebuffers.append(framebuffer)
        # DEP: cbuf, begin of renderpass

    shaders = ShaderLoader(gpu.device, dir ++ "glsl")
    # DEP: pipeline

    pipeline = pass.p0(shaders.fetch({
        vertex = "vertex_shader.vert.spv:main"
        fragment = "fragment_shader.frag.spv:main"
    }), pipeline_layout, vertex_layout) # extra={}...
    gpu.createPipeline(pipeline) # or pipelines.
    # DEP: cbuf

    portal = Portal(gpu, swapchain)

    frame_statistics = []
    frame_was = time()
    render = ():
        if not portal.acquire().SUCCESS
            return

        frame_now = time()
        frame_statistics.append(frame_now - frame_was)
        frame_was := frame_now

        cbuf.begin({flags = "ONE_TIME_SUBMIT_BIT"})
        cbuf.beginRenderPass({
            renderPass = pass
            framebuffer = portal.framebuffer
            renderArea = screen.area
            clearValues = [
                {color = {float32 = [
                    sin(time()) * 0.5 + 0.5,
                    sin(time() + 2*pi * 1 / 3) * 0.5 + 0.5,
                    sin(time() + 2*pi * 2 / 3) * 0.5 + 0.5,
                    1.0
                ]}}]
        }, "INLINE")
        cbuf.bindPipeline("GRAPHICS", pipeline)
        cbuf.bindDescriptorSets("GRAPHICS", pipeline_layout, 0, [dsc0], [])
        cbuf.bindVertexBuffers(0, [vbo.buffer], [0])
        cbuf.draw(6, 1, 0, 0)
        cbuf.endRenderPass()
        cbuf.end()
        portal.present([cbuf])

        if frame_statistics.length >= 1000
            best  = 1.0
            worst = 0.0
            avg   = 0.0
            for stat in frame_statistics
                best = min(best, stat)
                worst = max(worst, stat)
                avg += stat
            avg /= frame_statistics.length
            print("best:", 1/best, "worst:", 1/worst, "avg:", 1/avg)
            frame_statistics := []

    event = ffi.automem(sdl.sdl.Event)
    running = true
    while running
        render()
        while sdl.sdl.PollEvent(event) != 0
            if event.type == sdl.sdl.QUIT
                running = false

    gpu.device.waitIdle()
    vbo.buffer.mem.free()
    ubo.buffer.mem.free()
    gpu.destroy()
